
  newtype Substitution a = Substitution [(String, a)]

  identity :: Substitution a
  identity = Substitution []

  mkSubstitution :: String -> a -> Substitution a
  mkSubstitution dom rng = Substitution [(dom, rng)]

  compose :: Substitution a -> Substitution a -> Substitution a
  compose (Substitution theta) (Substitution theta') = Substitution $ theta ++ theta'

  --
  -- ** Pretty printings substitutions
  --

  instance Pretty a => Pretty (Substitution a) where
    pretty (Substitution []) = "id"
    pretty (Substitution ss) = "[" ++ body ++ "]"
      where
        body :: String
        body = L.intercalate ", " $ map (\(d, r) -> d ++ " := " ++ pretty r) ss

  --
  -- ** Type substitutions
  --

  type TypeSubstitution = Substitution Type

  typeSubst :: TypeSubstitution -> Type -> Type
  typeSubst (Substitution theta) (TyVar v) = theta `at` v
    where
      at :: [(String, Type)] -> String -> Type
      at []          v = TyVar v
      at ((d, r):xs) v
        | d == v    = r
        | otherwise = at xs v
  typeSubst theta (TyOperator descr args) = TyOperator descr $ map (typeSubst theta) args

  --
  -- ** Term substitutions
  --

  type TermSubstitution = Substitution Term

  termSubst' :: Term -> String -> Term -> Term
  termSubst' (Var v ty) dom rng
    | dom == v  = rng
    | otherwise = Var v ty
  termSubst' c@Const{} dom rng = c
  termSubst' (App l r) dom rng = App (termSubst' l dom rng) (termSubst' r dom rng)
  termSubst' (Lam a ty bdy) dom rng
    | a == dom || a `S.member` fv rng =
      let seen = S.unions [unqualifiedNamesOfTerm bdy, unqualifiedNamesOfTerm rng, S.singleton dom] in
      let fresh = qualifiedNameHead $ freshQualifiedName [] (Just a) seen in
        Lam fresh ty (termSubst' (permute a fresh bdy) dom rng)
    | otherwise = Lam a ty (termSubst' bdy dom rng)

  termSubst :: TermSubstitution -> Term -> Term
  termSubst (Substitution [])          t = t
  termSubst (Substitution ((d, r):xs)) t = termSubst (Substitution xs) (termSubst' t d r)

  termTypeSubst :: TypeSubstitution -> Term -> Term
  termTypeSubst theta (Var v ty)    = Var v $ typeSubst theta ty
  termTypeSubst theta (Const descr) = Const $ go descr
    where
      go :: ConstantDescription -> ConstantDescription
      go (PrimitiveConstant n ty) = PrimitiveConstant n (typeSubst theta ty)
      go (DefinedConstant n ty d) = DefinedConstant n (typeSubst theta ty) d
  termTypeSubst theta (App l r)      = App (termTypeSubst theta l) (termTypeSubst theta r)
  termTypeSubst theta (Lam a ty bdy) = Lam a (typeSubst theta ty) $ termTypeSubst theta bdy

--
  -- * Development of HOL within Caracal.  Move elsewhere when we have a
  --   clear idea of what a theory is.
  --

  --
  -- ** Truth and related concepts
  --

  -- |Definition of logical truth.
  trueDecl :: Inference (Term, Theorem)
  trueDecl = do
    let b  = mkVar "a" boolType
    let t  = mkLam "a" boolType b
    eq     <- mkEquality t t
    newDefinedConstant (mkQualifiedName ["Caracal", "Bool"] "true") boolType eq

  trueC :: Inference Term
  trueC = constantOfDecl trueDecl

  trueD :: Inference Theorem
  trueD = theoremOfDecl trueDecl

  -- |Produces a derivation of @{} ⊢ true@.
  trueI :: Inference Theorem
  trueI = do
    trueD <- trueD
    symmT <- symmetry trueD
    let b = mkVar "a" boolType
    let t = mkLam "a" boolType b
    reflT <- reflexivity t
    equalityModusPonens symmT reflT

  -- |Produces a derivation of @Gamma ⊢ p@ from a derivation of
  --  @Gamma ⊢ p = true@.
  trueE :: Theorem -> Inference Theorem
  trueE thm = do
    trueT <- trueI
    symmT <- symmetry thm
    equalityModusPonens symmT trueT

  -- |Produces a derivation of @Gamma ⊢ p@ from a derivation of
  --  @Gamma ⊢ true = p@.
  trueE' :: Theorem -> Inference Theorem
  trueE' thm = do
    symmT <- symmetry thm
    trueE symmT

  --
  -- ** Conjunction
  --

  -- |Definition of logical conjunction.
  conjunctionDecl :: Inference (Term, Theorem)
  conjunctionDecl = do
    trueC      <- trueC
    rightBody' <- mkApp (mkVar "f" binaryConnectiveType) trueC -- f true
    rightBody  <- mkApp rightBody' trueC -- f true true
    let right  =  mkLam "f" binaryConnectiveType rightBody -- \f. (f true true)
    leftBody'  <- mkApp (mkVar "f" binaryConnectiveType) (mkVar "p" boolType) -- f p
    leftBody   <- mkApp leftBody' (mkVar "q" boolType) -- f p q
    let left   =  mkLam "f" binaryConnectiveType leftBody -- \f. f p q
    eq         <- mkEquality left right -- \f. f p q = \f. f true true
    let def    =  mkLam "p" boolType (mkLam "q" boolType eq)
    newDefinedConstant (mkQualifiedName ["Caracal", "Bool"] "_∧_") binaryConnectiveType def

  conjunctionC :: Inference Term
  conjunctionC = constantOfDecl conjunctionDecl

  conjunctionT :: Inference Theorem
  conjunctionT = theoremOfDecl conjunctionDecl

  -- |Constructs a conjunction from two terms.
  mkConjunction :: Term -> Term -> Inference Term
  mkConjunction left right = do
    conjunctionC <- conjunctionC
    pre          <- mkApp conjunctionC left
    mkApp pre right

  -- |Produces a derivation of @Gamma u Delta ⊢ p ∧ q@ from a pair of
  --  derivations of @Gamma ⊢ p@ and @Delta ⊢ q@.
  conjunctionI :: Theorem -> Theorem -> Inference Theorem
  conjunctionI left right = undefined

  -- |Produces a derivation of @Gamma ⊢ p@ from a derivation of
  --  @Gamma ⊢ p ∧ q@.
  conjunctionELeft :: Theorem -> Inference Theorem
  conjunctionELeft thm = undefined

  -- |Produces a derivation of @Gamma ⊢ q@ from a derivation of
  --  @Gamma ⊢ p ∧ q@.
  conjunctionERight :: Theorem -> Inference Theorem
  conjunctionERight thm = undefined

  --
  -- ** Implication
  --

  implicationDecl :: Inference (Term, Theorem)
  implicationDecl = do
    conjunction <- mkConjunction (mkVar "p" boolType) (mkVar "q" boolType)
    body        <- mkEquality conjunction (mkVar "p" boolType)
    let def     =  mkLam "p" boolType (mkLam "q" boolType body)
    newDefinedConstant (mkQualifiedName ["Caracal", "Bool"] "_⇒_") binaryConnectiveType def

  implicationC :: Inference Term
  implicationC = constantOfDecl implicationDecl

  implicationT :: Inference Theorem
  implicationT = theoremOfDecl implicationDecl

  mkImplication :: Term -> Term -> Inference Term
  mkImplication left right = do
    implicationC <- implicationC
    pre          <- mkApp implicationC left
    mkApp pre right

  --
  -- ** Universal quantification
  --

  -- \P : a -> Bool. P = \x. T
  universalDecl :: Inference (Term, Theorem)
  universalDecl = do
    trueC     <- trueC
    let right = mkLam "a" alphaType trueC
    let left  = mkVar "P" (mkFunctionType alphaType boolType)
    eq        <- mkEquality left right
    let def   = mkLam "P" (mkFunctionType alphaType boolType) eq
    newDefinedConstant (mkQualifiedName ["Caracal", "Bool"] "∀") quantifierType def

  universalC :: Inference Term
  universalC = constantOfDecl universalDecl

  universalT :: Inference Theorem
  universalT = theoremOfDecl universalDecl

  mkUniversal :: Term -> Inference Term
  mkUniversal body = do
    universalC <- universalC
    mkApp universalC body

  --
  -- ** Logical disjunction
  --

  -- \P, Q. forall r. (P ==> r) ==> (Q == r) ==> r
  -- XXX: bug here somewhere
  disjunctionDecl :: Inference (Term, Theorem)
  disjunctionDecl = do
    impT1     <- mkImplication (mkVar "P" boolType) (mkVar "r" boolType)
    impT2     <- mkImplication (mkVar "Q" boolType) (mkVar "r" boolType)
    left      <- mkImplication impT1 impT2
    body      <- mkImplication left (mkVar "r" boolType)
    let lam   =  mkLam "r" boolType body
    universal <- mkUniversal lam
    let def   =  mkLam "P" boolType (mkLam "Q" boolType universal)
    newDefinedConstant (mkQualifiedName ["Caracal", "Bool"] "_∨_") binaryConnectiveType def

  disjunctionC :: Inference Term
  disjunctionC = constantOfDecl disjunctionDecl

  disjunctionT :: Inference Theorem
  disjunctionT = theoremOfDecl disjunctionDecl

  --
  -- ** Logical false
  --

  falseDecl :: Inference (Term, Theorem)
  falseDecl = do
    def <- mkUniversal (mkLam "a" boolType $ mkVar "a" boolType)
    newDefinedConstant (mkQualifiedName ["Caracal", "Bool"] "false") boolType def

  falseC :: Inference Term
  falseC = constantOfDecl falseDecl

  falseT :: Inference Theorem
  falseT = theoremOfDecl falseDecl